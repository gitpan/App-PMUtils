#!perl

use 5.010;
use strict;
use warnings;
use Log::Any '$log';

use App::PMUtils;
use Perinci::CmdLine::Lite;

our $DATE = '2014-07-29'; # DATE
our $VERSION = '0.19'; # VERSION

our %SPEC;
$SPEC{pminfo} = {
    v => 1.1,
    summary => 'Get Perl module information',
    description => <<'_',

Will show this information about each Perl module:

* path
* version
* number of lines of code
*

_
    args => {
        module => {
            schema => ['array*' => of => 'str*', min_len=>1],
            req => 1,
            pos => 0,
            greedy => 1,
            element_completion => $App::PMUtils::_complete_module,
        },
    },
    result_naked => 1,
};
sub pminfo {
    require Module::Load;
    require SHARYANTO::Module::Path;

    my %args = @_;
    my $mods = $args{module};

    my $res = [];
    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        my $rec = {module=>$mod};
        push @$res, $rec;
        my $mpath = SHARYANTO::Module::Path::module_path(module=>$mod);
        $rec->{path} = $mpath;
        next unless $mpath;
        # load to get $VERSION & $DATE
        {
            no strict 'refs';
            Module::Load::load($mod);
            $rec->{version} = ${"$mod\::VERSION"};
            $rec->{date} = ${"$mod\::DATE"};
        }
        if (open my($fh), "<", $mpath) {
            my $lines = 0; $lines++ while <$fh>;
            $rec->{lines} = $lines;
        }
    }

    $res;
}

Perinci::CmdLine::Lite->new(
    url => '/main/pminfo',
)->run;

# ABSTRACT: Get Perl module information
# PODNAME: pminfo

__END__

=pod

=encoding UTF-8

=head1 NAME

pminfo - Get Perl module information

=head1 VERSION

This document describes version 0.19 of pminfo (from Perl distribution App-PMUtils), released on 2014-07-29.

=head1 SYNOPSIS

 % pminfo Some::Module Another::Module

To active bash completion:

 % complete -C pminfo pminfo ; # can be put in bash startup file e.g. .bashrc
 % pminfo Some<tab>

 % pminfo test/builder/mo<tab> ; # resolve to Test/Builder/Module, can resolve case
 % pminfo "Text::Abb<tab>      ; # use quote (' or ") if you want to use :: as separator

=head1 SEE ALSO

L<pmversion>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-App-PMUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

Steven Haryanto <stevenharyanto@gmail.com>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2014 by Steven Haryanto.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
