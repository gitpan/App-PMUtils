#!perl

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;
use SHARYANTO::Module::Path;

our $DATE = '2014-08-17'; # DATE
our $VERSION = '0.21'; # VERSION

our %SPEC;
$SPEC{pmpath} = {
    v => 1.1,
    summary => 'Get path to locally installed Perl module',
    args => {
        module => {
            schema => ['array*', of=>'str*', min_len=>1],
            req    => 1,
            pos    => 0,
            greedy => 1,
            element_completion => $App::PMUtils::_complete_module,
        },
        all => {
            schema => 'bool',
            cmdline_aliases => {a=>{}},
        },
        abs => {
            schema => 'bool',
            cmdline_aliases => {P=>{}},
        },
    },
    "x.perinci.cmdline.default_format" => 'text-simple',
};
sub pmpath {
    require SHARYANTO::Module::Path;
    my %args = @_;

    my $mods = $args{module};
    my $res = [];
    my $found;

    for my $mod (@{$mods}) {
        my $mpath = SHARYANTO::Module::Path::module_path(
            module=>$mod, find_pmc=>0, find_pod=>0, find_prefix=>1,
            abs=>$args{abs}, all=>$args{all});
        $found++ if $mpath;
        push @$res, @$mods > 1 ? {module=>$mod, path=>$mpath} : $mpath;
    }

    if ($found) {
        [200, "OK", $res];
    } else {
        [404, "No such module"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/pmpath',
)->run;

# ABSTRACT: Get path to locally installed Perl module
# PODNAME: pmpath

__END__

=pod

=encoding UTF-8

=head1 NAME

pmpath - Get path to locally installed Perl module

=head1 VERSION

This document describes version 0.21 of pmpath (from Perl distribution App-PMUtils), released on 2014-08-17.

=head1 SYNOPSIS

Basic usage:

 % pmpath Some::Module

Find all modules (-a), return absolute paths (-P):

 % pmpath -a Some::Module

See all other options:

 % pmpath --help

To active bash completion:

 % complete -C pmpath pmpath ; # can be put in bash startup file e.g. .bashrc
 % pmpath Some<tab>
 % pmpath test/builder/mo<tab> ; # resolve to Test/Builder/Module, can resolve case
 % pmpath "Text::Abb<tab>      ; # use quote (' or ") if you want to use :: as separator

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-App-PMUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

Steven Haryanto <stevenharyanto@gmail.com>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2014 by Steven Haryanto.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
